&НаКлиенте
Перем мМесяцБюджета;

&НаСервере
Функция ПолучитьРабочийБюджет()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетГод.Ссылка
		|ИЗ
		|	Документ.БюджетГод КАК БюджетГод
		|ГДЕ
		|	БюджетГод.Дата Между &ДатаНач И &ДатаКон
		|	И НЕ БюджетГод.ПометкаУдаления
		|	И БюджетГод.ВидБюджета = &ВидБюджета";

	Запрос.УстановитьПараметр("ВидБюджета", Перечисления.ВидыБюджетов.Рабочий);
	Запрос.УстановитьПараметр("ДатаНач", НачалоГода(ТекущаяДата()));
	Запрос.УстановитьПараметр("ДатаКон", КонецГода(ТекущаяДата()));

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;

	Возврат Неопределено;

КонецФункции

&НаСервере
Функция СформироватьСписокТранзакций(СтатьяДР, МесяцБюджета)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БДРОбороты.Период КАК Дата,
		|	СУММА(БДРОбороты.СуммаФакт) КАК Сумма,
		|	БДРОбороты.СтатьяДоходовРасходов КАК Категория,
		|	БДРОбороты.Регистратор.Номер КАК Номер,
		|	БДРОбороты.Регистратор.Дата КАК ДатаДок,
		|	ТИПЗНАЧЕНИЯ(БДРОбороты.Регистратор) КАК ВидДок
		|ИЗ
		|	РегистрНакопления.БДР КАК БДРОбороты
		|ГДЕ
		|	БДРОбороты.Период МЕЖДУ &НачПериода И &КонПериода
		|	И БДРОбороты.СтатьяДоходовРасходов = &СтатьяДР
		|
		|СГРУППИРОВАТЬ ПО
		|	БДРОбороты.Период,
		|	БДРОбороты.СтатьяДоходовРасходов,
		|	БДРОбороты.Регистратор.Номер,
		|	БДРОбороты.Регистратор.Дата,
		|	ТИПЗНАЧЕНИЯ(БДРОбороты.Регистратор)
		|
		|ИМЕЮЩИЕ
		|	СУММА(БДРОбороты.СуммаФакт) <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата";

	Запрос.УстановитьПараметр("СтатьяДР", СтатьяДР);
	Запрос.УстановитьПараметр("НачПериода", НачалоМесяца(МесяцБюджета));
	Запрос.УстановитьПараметр("КонПериода", КонецМесяца(МесяцБюджета));

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Транзакции.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовСтр = Транзакции.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
	КонецЦикла;

	Возврат Неопределено;

КонецФункции

&НаСервере
Функция ДобавитьКолонкуДереваСтатьяДРСервер(ИмяКолонки =  "СтатьяДР", ЗаголовокКолонки="Статья ДР")
	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("Строка"));
	ТипыРеквизита.Добавить(Тип("СправочникСсылка.СтатьиДоходовРасходов"));
	
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита);
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяКолонки,//имя
	ОписаниеТиповДляРеквизита,//тип
	"ДеревоБДР",                 //путь
	ЗаголовокКолонки,                      //заголовок
	Ложь                           //сохраняемые данные
	);
	
	Возврат НовыйРеквизит;
	
КонецФункции

//для добавления колонок План, Факт, Рез
&НаСервере
Функция ДобавитьКолонкуДереваТипЧислоСервер(ИмяКолонки, ЗаголовокКолонки, Размерность=10,Точность=0)
	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("Число"));
	
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита, Новый КвалификаторыЧисла(Размерность,Точность));
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяКолонки,//имя
	ОписаниеТиповДляРеквизита,//тип
	"ДеревоБДР",                 //путь
	ЗаголовокКолонки,                      //заголовок
	Ложь                           //сохраняемые данные
	);
	
	Возврат НовыйРеквизит;
	
КонецФункции

//для добавления колонок ЭтоГруппа
&НаСервере
Функция ДобавитьКолонкуДереваТипБулевоСервер(ИмяКолонки, ЗаголовокКолонки)
	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("Булево"));
	
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита);
	
	НовыйРеквизит = Новый РеквизитФормы(ИмяКолонки,//имя
	ОписаниеТиповДляРеквизита,//тип
	"ДеревоБДР",                 //путь
	ЗаголовокКолонки,                      //заголовок
	Ложь                           //сохраняемые данные
	);
	
	Возврат НовыйРеквизит;
	
КонецФункции


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//начинаем добавлять колонки таблицы на форму
	ДобавляемыеРеквизиты = Новый Массив;
	
	//колонка СтатьяДР
	ДобавляемыеРеквизиты.Добавить(ДобавитьКолонкуДереваСтатьяДРСервер());

	//служебные колонки
	ДобавляемыеРеквизиты.Добавить(ДобавитьКолонкуДереваТипБулевоСервер("ЭтоГруппа","ЭтоГруппа"));
	ДобавляемыеРеквизиты.Добавить(ДобавитьКолонкуДереваТипЧислоСервер("ПриходРасход", "Приход расход",1));

	//колонки плана и факта по месяцам
	Для сч = 1 По 13 Цикл
		ДобавляемыеРеквизиты.Добавить(ДобавитьКолонкуДереваТипЧислоСервер("План"+сч,"План"));//колонка ПланХ
		ДобавляемыеРеквизиты.Добавить(ДобавитьКолонкуДереваТипЧислоСервер("Факт"+сч,"Факт"));
		ДобавляемыеРеквизиты.Добавить(ДобавитьКолонкуДереваТипЧислоСервер("Рез"+сч,"%",3));
	КонецЦикла;
	
	//выполняем изменение формы - добавляем колонки
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ЗаполнитьДеревоБДРСервер();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоБДРСервер()
	
    ДеревоБДРФорма = ДанныеФормыВЗначение(ДеревоБДР, Тип("деревоЗначений"));
	СтруктураПараметры	 = Новый Структура ();
	СтруктураПараметры.Вставить("ДеревоБДРФорма",ДеревоБДРФорма);
	СтруктураПараметры.Вставить("НачПериода",НачалоГода(ТекущаяДата()));
	СтруктураПараметры.Вставить("КонПериода",КонецГода(ТекущаяДата()));
	
	СтруктураПараметры.Вставить("Объект",ПолучитьРабочийБюджет());
	
	НачОст = ОбщийМодуль1Сервер.ВернутьОстатокСредствСервер(НачалоГода(ТекущаяДата()));
	СтруктураПараметры.Вставить("НачальныйОстатокПлан",НачОст);
	СтруктураПараметры.Вставить("НачальныйОстатокФакт",НачОст);
	
	ОбщийМодуль1Сервер.ЗаполнитьДеревоИзСправочникаСервер(СтруктураПараметры);
	
	ЗначениеВДанныеФормы(ДеревоБДРФорма, ДеревоБДР);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокКатегорийСервер(ПериодМесяц =  Неопределено)
	
	ДеревоБДРФорма 		 = ДанныеФормыВЗначение(ДеревоБДР, Тип("деревоЗначений"));
	КатегорииДерево		 = ДанныеФормыВЗначение(Категории, Тип("ДеревоЗначений"));
	
	КатегорииДерево.Строки.Очистить();
	
	ДобавитьВложенныеСтроки(ДеревоБДРФорма.Строки, КатегорииДерево.Строки, ПериодМесяц);

	ЗначениеВДанныеФормы(КатегорииДерево, Категории);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокКатегорийКлиент()
	
	Категории.ПолучитьЭлементы().Очистить();
	
	ДобавитьВложенныеСтрокиКлиент(ДеревоБДР.ПолучитьЭлементы(), Категории.ПолучитьЭлементы(), мМесяцБюджета);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВложенныеСтроки(Строки, НовСтр, ПериодМесяц)
	Для каждого Стр Из Строки Цикл
		НовСтр1 			= НовСтр.Добавить();
		НовСтр1.Категория 	= Стр.СтатьяДР;
		Если ПериодМесяц = Неопределено Тогда
			НовСтр1.План 		= Стр["План"+Месяц(ТекущаяДата())];
			НовСтр1.Факт 		= Стр["Факт"+Месяц(ТекущаяДата())];
		Иначе
			НовСтр1.План 		= Стр["План"+Месяц(ПериодМесяц)];
			НовСтр1.Факт 		= Стр["Факт"+Месяц(ПериодМесяц)];
		КонецЕсли;
		НовСтр1.ЭтоГруппа 	= Стр.ЭтоГруппа;
		//НовСтр1.Рез = "||||||||||||||||||||||||||||||||||||||||||||||||||";
		ПроцентВыполнения = ?(НовСтр1.План=0, 0, Окр(НовСтр1.Факт/НовСтр1.План,2,1))*100;
		//102 - чтобы вобще палочек не было, если процент 0, если будет 100 - будет оставаться 1 палочка
		НовСтр1.Рез = Сред("||||||||||||||||||||||||||||||||||||||||||||||||||", (102-ПроцентВыполнения)/2)+" "+Строка(ПроцентВыполнения)+"%";
		ДобавитьВложенныеСтроки(Стр.Строки, НовСтр1.Строки, ПериодМесяц);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложенныеСтрокиКлиент(Строки, НовСтр, ПериодМесяц)
	Для каждого Стр Из Строки Цикл
		НовСтр1 			= НовСтр.Добавить();
		НовСтр1.Категория 	= Стр.СтатьяДР;
		Если ПериодМесяц = Неопределено Тогда
			НовСтр1.План 		= Стр["План"+Месяц(ТекущаяДата())];
			НовСтр1.Факт 		= Стр["Факт"+Месяц(ТекущаяДата())];
		Иначе
			НовСтр1.План 		= Стр["План"+Месяц(ПериодМесяц)];
			НовСтр1.Факт 		= Стр["Факт"+Месяц(ПериодМесяц)];
		КонецЕсли;
		НовСтр1.ЭтоГруппа 	= Стр.ЭтоГруппа;
		//НовСтр1.Рез = "||||||||||||||||||||||||||||||||||||||||||||||||||";
		ПроцентВыполнения = ?(НовСтр1.План=0, 0, Окр(НовСтр1.Факт/НовСтр1.План,2,1))*100;
		//102 - чтобы вобще палочек не было, если процент 0, если будет 100 - будет оставаться 1 палочка
		НовСтр1.Рез = Сред("||||||||||||||||||||||||||||||||||||||||||||||||||", (102-ПроцентВыполнения)/2)+" "+Строка(ПроцентВыполнения)+"%";
		ДобавитьВложенныеСтрокиКлиент(Стр.ПолучитьЭлементы(), НовСтр1.ПолучитьЭлементы(), ПериодМесяц);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КатегорииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаДерева = Категории.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если НЕ СтрокаДерева.ЭтоГруппа Тогда
		//показать  список транзакций по выбранной статье ДДС
		СформироватьСписокТранзакций(Элемент.ТекущиеДанные.Категория, мМесяцБюджета);
		
		//показать нужную страницу
		УправлениеВидимостьюКлиент(2);
		
	Иначе
		
		Если Элемент.Развернут(ВыбраннаяСтрока) Тогда
			Элемент.Свернуть(ВыбраннаяСтрока);
		Иначе
			Элемент.Развернуть(ВыбраннаяСтрока);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	мМесяцБюджета = НачалоМесяца(ТекущаяДата());

	ЗаполнитьСписокКатегорийКлиент();
	УправлениеВидимостьюКлиент(1);
	Элементы.ОК.Доступность = Ложь;
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Функция ИмяМесяцаПоНомеру(НомерМесяца)
    Возврат НРег(Формат(ДобавитьМесяц('19700101', НомерМесяца-1), "ДФ=MMMM"));
КонецФункции

&НаКлиенте
Процедура УстановитьЗаголовокФормы()
	ЭтаФорма.АвтоЗаголовок = Ложь;
	ЭтаФорма.Заголовок = "Бюджет (месяц) - "+ИмяМесяцаПоНомеру(Месяц(мМесяцБюджета))+" "+СтрЗаменить(Год(мМесяцБюджета),Символы.НПП,"");
КонецПроцедуры


&НаКлиенте
Процедура Назад(Команда)
	УправлениеВидимостьюКлиент(1);	
КонецПроцедуры

&НаКлиенте
Процедура ПовторятьПриИзменении(Элемент)
	ВидимостьПериодичностиКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьПериодичностиКлиент()
	Элементы.Декорация1.Видимость 		= Повторять;
	Элементы.Периодичность.Видимость 	= Повторять;
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостьюКлиент(НомерСлоя = 1)
	Если НомерСлоя = 1 Тогда
		Элементы.Слой1.Видимость = Истина;
		Элементы.Слой2.Видимость = Ложь;
		Элементы.Слой3.Видимость = Ложь;
	ИначеЕсли НомерСлоя = 2 Тогда
		Элементы.Слой1.Видимость = Ложь;
		Элементы.Слой2.Видимость = Истина;
		Элементы.Слой3.Видимость = Ложь;
		Элементы.ЗаголовокСлой2.Заголовок = Категории.НайтиПоИдентификатору(Элементы.Категории.ТекущаяСтрока).Категория;//Элементы.Категории.ТекущиеДанные.Категория;
	ИначеЕсли НомерСлоя = 3 Тогда
		Элементы.Слой1.Видимость = Ложь;
		Элементы.Слой2.Видимость = Ложь;
		Элементы.Слой3.Видимость = Истина;
		
		ВидимостьПериодичностиКлиент();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТранзакцииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Дата 		= Элемент.ТекущиеДанные.Дата;
	Сумма		= Элемент.ТекущиеДанные.Сумма;
	Категория 	= Элемент.ТекущиеДанные.Категория;
	УправлениеВидимостьюКлиент(3);
КонецПроцедуры


&НаКлиенте
Процедура ОК(Команда)
	УправлениеВидимостьюКлиент(2);
КонецПроцедуры


&НаКлиенте
Процедура Отмена(Команда)
	УправлениеВидимостьюКлиент(2);
КонецПроцедуры


&НаКлиенте
Процедура ТранзакцииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ 		= Истина;
	Дата 		= ТекущаяДата();
	Сумма 		= 0;
	Категория 	= Категории.НайтиПоИдентификатору(Элементы.Категории.ТекущаяСтрока).Категория;//Элементы.Категории.ТекущиеДанные.Категория;
	УправлениеВидимостьюКлиент(3);
	//Элементы.Категории.ДанныеСтроки(Элементы.Категории.ТекущаяСтрока)
КонецПроцедуры


&НаКлиенте
Процедура Редактировать(Команда)
	Элементы.ОК.Доступность = Истина;
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ЗаполнитьДеревоБДРСервер();
	ЗаполнитьСписокКатегорийКлиент();
КонецПроцедуры


&НаКлиенте
Процедура МесяцНазад(Команда)
	
	мМесяцБюджета = ДобавитьМесяц(мМесяцБюджета,-1);
	//ЗаполнитьСписокКатегорийСервер(мМесяцБюджета);
	ЗаполнитьСписокКатегорийКлиент();
	УстановитьЗаголовокФормы();
	
КонецПроцедуры


&НаКлиенте
Процедура МесяцВперед(Команда)
	мМесяцБюджета = ДобавитьМесяц(мМесяцБюджета,1);
	//ЗаполнитьСписокКатегорийСервер(мМесяцБюджета);
	ЗаполнитьСписокКатегорийКлиент();
	УстановитьЗаголовокФормы();
КонецПроцедуры

