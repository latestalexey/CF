
&НаКлиенте
Процедура СформироватьОтчет(Команда)
	СформироватьОтчетСервер();
	
КонецПроцедуры

&НаСервере
Функция СформироватьЧастьОтчета(Макет, ЗаголовокРаздела, Итого, ТекстЗапроса)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Если ЗначениеЗаполнено(ДатаБаланса) Тогда
		Запрос.УстановитьПараметр("ДатаБаланса", КонецДня(ДатаБаланса));
	Иначе		
		Запрос.УстановитьПараметр("ДатаБаланса", КонецДня(ТекущаяДата()));
	КонецЕсли;
		
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Область = Макет.ПолучитьОбласть("РазделЗагол");
	Область.Параметры.ЗаголовокРаздела = ЗаголовокРаздела;
	Область.Параметры.Сумма = ТЗ.Итог("СуммаОстаток");
	Результат.Вывести(Область);
	Итого = Итого + ТЗ.Итог("СуммаОстаток");
	
	Результат.НачатьГруппуСтрок("А1",Ложь);
	Для каждого Стр Из ТЗ Цикл
		Область = Макет.ПолучитьОбласть("РазделДетал");
		Область.Параметры.Детализация = Стр.Детализация;
		Область.Параметры.Сумма = Стр.СуммаОстаток;
		Результат.Вывести(Область);
	КонецЦикла;
	Результат.ЗакончитьГруппуСтрок();	
КонецФункции


&НаСервере
Функция СформироватьОтчетСервер()
	
	Результат.Очистить();
	
	Макет = Отчеты.Баланс2.ПолучитьМакет("Макет");
	Область = Макет.ПолучитьОбласть("Шапка");
	Результат.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	Область.Параметры.Заголовок = "АКТИВ";
	Результат.Вывести(Область);
	
	ИтогоАктив = 0;
	
	//1. Собственные средствва
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	лимиты.Кошелек,
	|	лимиты.Лимит
	|ПОМЕСТИТЬ ВТлимиты
	|ИЗ
	|	РегистрСведений.ЛимитыОвердрафта.СрезПоследних(&ДатаБаланса, ) КАК лимиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДенежныеСредстваОстатки.Кошелек,
	|	ДенежныеСредстваОстатки.СуммаРеглОстаток
	|ПОМЕСТИТЬ ВТКредиты
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.Остатки(&ДатаБаланса, Кошелек.КредитнаяКарта) КАК ДенежныеСредстваОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХХХ.Кошелек,
	|	СУММА(ХХХ.СуммаРеглОстаток) КАК СуммаРеглОстаток
	|ПОМЕСТИТЬ ВТСобственные
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТКредиты.Кошелек КАК Кошелек,
	|		ВТКредиты.СуммаРеглОстаток КАК СуммаРеглОстаток
	|	ИЗ
	|		ВТКредиты КАК ВТКредиты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТлимиты.Кошелек,
	|		-ВТлимиты.Лимит
	|	ИЗ
	|		ВТлимиты КАК ВТлимиты) КАК ХХХ
	|
	|СГРУППИРОВАТЬ ПО
	|	ХХХ.Кошелек
	|
	|ИМЕЮЩИЕ
	|	СУММА(ХХХ.СуммаРеглОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДенежныеСредстваОстатки.Кошелек КАК Детализация,
	|	ДенежныеСредстваОстатки.СуммаРеглОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.Остатки(
	|			&ДатаБаланса,
	|			НЕ Кошелек.КредитнаяКарта
	|				И Кошелек.РасчетОстатковБюджет) КАК ДенежныеСредстваОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСобственные.Кошелек,
	|	ВТСобственные.СуммаРеглОстаток
	|ИЗ
	|	ВТСобственные КАК ВТСобственные";
	
	СформироватьЧастьОтчета(Макет, "Собственные средства", ИтогоАктив, ТекстЗапроса);
		
	//2. займы выданные
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Рег.СуммаРеглОстаток КАК СуммаОстаток,
	               |	Рег.Контрагент КАК Детализация
	               |ИЗ
	               |	РегистрНакопления.Долги.Остатки(&ДатаБаланса, ВидДолга = ЗНАЧЕНИЕ(Перечисление.ВидыДолга.Мне)) КАК Рег";
				   
	СформироватьЧастьОтчета(Макет, "Займы выданные", ИтогоАктив, ТекстЗапроса);

	//3. сбережения
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Рег.СуммаРеглОстаток КАК СуммаОстаток,
	               |	Рег.Кошелек КАК Детализация
	               |ИЗ
	               |	РегистрНакопления.Сбережения.Остатки(&ДатаБаланса) КАК Рег";
	СформироватьЧастьОтчета(Макет, "Сбережения", ИтогоАктив, ТекстЗапроса);
		
	
	//конец активов		
	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.Сумма = ИтогоАктив;
	Результат.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Пробел");
	Результат.Вывести(Область);
	
	
	//ПАССИВ
	Область = Макет.ПолучитьОбласть("Заголовок");
	Область.Параметры.Заголовок = "ПАССИВ";
	Результат.Вывести(Область);

	ИтогоПассив = 0;
	
	//1. Займы полученные
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Рег.СуммаОстаток КАК СуммаОстаток,
	               |	Рег.Контрагент КАК Детализация
	               |ИЗ
	               |	РегистрНакопления.Долги.Остатки(&ДатаБаланса, ВидДолга = ЗНАЧЕНИЕ(Перечисление.ВидыДолга.Мой)) КАК Рег";
		
	СформироватьЧастьОтчета(Макет, "Займы полученные", ИтогоПассив, ТекстЗапроса);
	
	//2. Кредиты полученные (в кредитных организациях)
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Рег.СуммаОстаток КАК СуммаОстаток,
		|	Рег.Кошелек КАК Детализация
		|ИЗ
		|	РегистрНакопления.Кредиты.Остатки(&ДатаБаланса) КАК Рег";
		
	СформироватьЧастьОтчета(Макет, "Кредиты полученные", ИтогоПассив, ТекстЗапроса);
	
	//конец пассивов
	
	Область = Макет.ПолучитьОбласть("Итого");
	Область.Параметры.Сумма = ИтогоПассив;
	Результат.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Пробел");
	Результат.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Баланс");
	Область.Параметры.Сумма = ИтогоАктив - ИтогоПассив;
	Результат.Вывести(Область);
	
	//Таб.ТолькоПросмотр = Истина;
	Возврат 0;

КонецФункции

