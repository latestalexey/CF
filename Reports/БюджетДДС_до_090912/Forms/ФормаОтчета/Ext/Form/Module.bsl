
&НаСервере
Функция РезультатОбработкаРасшифровкиСервер(Расшифровка)
	
	Перем ВыполненноеДействие;
	 
	ОтчетСервер = ДанныеФормыВЗначение(Отчет, Тип("ОтчетОбъект.БюджетДДС"));
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(ОтчетСервер.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"));
    ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, ИсточникДоступныхНастроек);
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	МассивПолей = Данные.Элементы[Расшифровка].ПолучитьПоля();
	ПолеРасшифровки = Новый ПолеКомпоновкиДанных(МассивПолей[0]);
	Настройки = ОбработкаРасшифровки.Расшифровать(Расшифровка, ПолеРасшифровки);
	
	Отбор = Новый Структура("Вариант, СтатьяДР, НачалоПериода, КонецПериода");
	
	Если Настройки = Неопределено Тогда
        Возврат Отбор;
	КонецЕсли;	
	
	флагПериодЗадан = Ложь;
	Бюджет = ОтчетСервер.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.найти("Бюджет").Значение;
	Отбор.Вставить("Бюджет", Бюджет);
	Для Каждого Эл Из Настройки.Отбор.Элементы Цикл
		
		Если Эл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтатьяДР") Тогда
			Отбор.Вставить("СтатьяДР", Эл.ПравоеЗначение);
			
		ИначеЕсли Эл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПериодДДС") Тогда
			
			Если ТипЗнч(Эл.ПравоеЗначение) = Тип("Дата") Тогда
				Отбор.Вставить("НачалоПериода", НачалоМесяца(Эл.ПравоеЗначение));
				Отбор.Вставить("КонецПериода",  КонецМесяца (Эл.ПравоеЗначение));
			Иначе
				//выбрали колоку "СтатьяДР"
				Отбор.Вставить("НачалоПериода", НачалоГода(Эл.ПравоеЗначение.Дата));
				Отбор.Вставить("КонецПериода",  КонецГода (Эл.ПравоеЗначение.Дата));
				
			КонецЕсли;
			флагПериодЗадан = Истина;
		КонецЕсли;
	КонецЦикла;	
	
	Если НЕ флагПериодЗадан Тогда
		//выбрали для расшифровки статью ДР, а в этом случае в Настройки.Отбор.Элементы нет полей периода
		Отбор.Вставить("НачалоПериода", НачалоГода(Бюджет.Дата));
		Отбор.Вставить("КонецПериода",  КонецГода (Бюджет.Дата));
	КонецЕсли;
	
	Возврат Отбор;
	
КонецФункции

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Перем ВыполненноеДействие;

	СтандартнаяОбработка = Ложь;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Ведомость по статьям");
	СписокВыбора.Добавить("План-факт за месяц");
	//Рез = СписокВыбора.ВыбратьЭлемент();
	Рез = ВыбратьИзМеню(СписокВыбора);
	Если Рез = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = РезультатОбработкаРасшифровкиСервер(Расшифровка);
	
	ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.КлючНазначенияИспользования = "РасшифровкаСтатьиБюджета";
	ПараметрыФормы.КлючВарианта = "Основной";
	
	ПараметрыФормы.Отбор = Отбор;
	
	Если Рез.Значение = "Ведомость по статьям" Тогда
		
		ОткрытьФорму("Отчет.ВедомостьПоСтатьямДР.Форма", ПараметрыФормы);
		
	Иначе
		
		ОткрытьФорму("Отчет.РасшифровкаБюджета.Форма", ПараметрыФормы);//07 09 11
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетСервер(ТекстОшибки =  "")
	
	Эл2 = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Бюджет");
	Эл2.Значение = Бюджет;
	Эл2.Использование = Истина;
	
	Эл2 = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НачалоПериода");
	Эл2.Значение = НачалоГода(Бюджет.Дата);
	Эл2.Использование = Истина;
	
	Эл2 = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("КонецПериода");
	Эл2.Значение = КонецГода(Бюджет.Дата);
	Эл2.Использование = Истина;
	
	// расходы за счет сбережений можно показывать отдельно, не в общей сумме расходов
	Эл2 = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("РасходыСберОтдельно");
	Эл2.Значение = РасходыСберОтдельно;
	Эл2.Использование = Истина;
	
	Для Каждого Эл Из Отчет.КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
		Если Эл.Поле = Новый ПолеКомпоновкиДанных("План") Тогда
			Эл.Использование = ПоляПлан;
		ИначеЕсли Эл.Поле = Новый ПолеКомпоновкиДанных("Факт") Тогда
			Эл.Использование = ПоляФакт;
		ИначеЕсли Эл.Поле = Новый ПолеКомпоновкиДанных("ВП") Тогда
			Эл.Использование = ПоляРез;
		ИначеЕсли Эл.Поле = Новый ПолеКомпоновкиДанных("Абс") Тогда
			Эл.Использование = ПоляАбс;
		КонецЕсли;
	КонецЦикла;
	
	//вывод отчета
	СкомпоноватьРезультат();	
	
	Результат.ПоказатьУровеньГруппировокСтрок(0);
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьОтчет(Команда)
	Если НЕ ЗначениеЗаполнено(Бюджет) Тогда
		Предупреждение("Необходимо выбрать бюджет!");	
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	Рез = СформироватьОтчетСервер(ТекстОшибки);
	Если НЕ Рез Тогда
		Предупреждение(ТекстОшибки, 120);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ВариантБюджетаПриИзменении(Элемент)
	ТекстОшибки = "";
	Рез = СформироватьОтчетСервер(ТекстОшибки);
	Если НЕ Рез Тогда
		Предупреждение(ТекстОшибки, 120);
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПоляПлан = Истина;
	ПоляФакт = Истина;
	ПоляРез = Истина;
КонецПроцедуры

