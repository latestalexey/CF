
&НаСервере
Функция РезультатОбработкаРасшифровкиСервер(Расшифровка)
	
	Перем ВыполненноеДействие;
	 
	ОтчетСервер = ДанныеФормыВЗначение(Отчет, Тип("ОтчетОбъект.БюджетДДС"));
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(ОтчетСервер.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"));
    ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, ИсточникДоступныхНастроек);
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	МассивПолей = Данные.Элементы[Расшифровка].ПолучитьПоля();
	ПолеРасшифровки = Новый ПолеКомпоновкиДанных(МассивПолей[0]);
	Настройки = ОбработкаРасшифровки.Расшифровать(Расшифровка, ПолеРасшифровки);
	
	Отбор = Новый Структура("Вариант, СтатьяДР, НачалоПериода, КонецПериода");
	
	Если Настройки = Неопределено Тогда
        Возврат Отбор;
	КонецЕсли;	
	
	флагПериодЗадан = Ложь;
	Бюджет = ОтчетСервер.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.найти("Бюджет").Значение;
	Отбор.Вставить("Бюджет", Бюджет);
	Для Каждого Эл Из Настройки.Отбор.Элементы Цикл
		
		Если Эл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтатьяДР") Тогда
			Отбор.Вставить("СтатьяДР", Эл.ПравоеЗначение);
			
		ИначеЕсли Эл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПериодДДС") Тогда
			
			Если ТипЗнч(Эл.ПравоеЗначение) = Тип("Дата") Тогда
				Отбор.Вставить("НачалоПериода", НачалоМесяца(Эл.ПравоеЗначение));
				Отбор.Вставить("КонецПериода",  КонецМесяца (Эл.ПравоеЗначение));
			Иначе
				//выбрали колоку "СтатьяДР"
				Отбор.Вставить("НачалоПериода", НачалоГода(Эл.ПравоеЗначение.Дата));
				Отбор.Вставить("КонецПериода",  КонецГода (Эл.ПравоеЗначение.Дата));
				
			КонецЕсли;
			флагПериодЗадан = Истина;
		КонецЕсли;
	КонецЦикла;	
	
	Если НЕ флагПериодЗадан Тогда
		//выбрали для расшифровки статью ДР, а в этом случае в Настройки.Отбор.Элементы нет полей периода
		Отбор.Вставить("НачалоПериода", НачалоГода(Бюджет.Дата));
		Отбор.Вставить("КонецПериода",  КонецГода (Бюджет.Дата));
	КонецЕсли;
	
	Возврат Отбор;
	
КонецФункции

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Перем ВыполненноеДействие;

	СтандартнаяОбработка = Ложь;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Ведомость по статьям");
	СписокВыбора.Добавить("План-факт за месяц");
	//Рез = СписокВыбора.ВыбратьЭлемент();
	Рез = ВыбратьИзМеню(СписокВыбора);
	Если Рез = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = РезультатОбработкаРасшифровкиСервер(Расшифровка);
	
	ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.КлючНазначенияИспользования = "РасшифровкаСтатьиБюджета";
	ПараметрыФормы.КлючВарианта = "Основной";
	
	ПараметрыФормы.Отбор = Отбор;
	
	Если Рез.Значение = "Ведомость по статьям" Тогда
		
		ОткрытьФорму("Отчет.ВедомостьПоСтатьямДР.Форма", ПараметрыФормы);
		
	Иначе
		
		ОткрытьФорму("Отчет.РасшифровкаБюджета.Форма", ПараметрыФормы);//07 09 11
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетСервер(ТекстОшибки =  "")
	
	ДеревоБДР = ОбщийМодуль1Сервер.СоздатьДеревоБДР();
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДеревоБДРФорма",ДеревоБДР);
	СтруктураПараметров.Вставить("Объект",План);
	СтруктураПараметров.Вставить("НачальныйОстатокПлан",План.НачальныйОстаток);
	СтруктураПараметров.Вставить("НачальныйОстатокФакт",?(ЗначениеЗаполнено(Факт), Факт.НачальныйОстаток, План.НачальныйОстаток));
	СтруктураПараметров.Вставить("НачПериода",НачалоГода(План.Дата));
	СтруктураПараметров.Вставить("КонПериода",КонецГода(План.Дата));
	СтруктураПараметров.Вставить("БюджетФакт",Факт);

	ОбщийМодуль1Сервер.ЗаполнитьДеревоИзСправочникаСервер(СтруктураПараметров);
	
	Если ПоляАбс Тогда
		Для сч = 1 По 13 Цикл
			ДеревоБДР.Колонки.Добавить("Абс"+сч);
		КонецЦикла;
		
		РасчетАбс(ДеревоБДР.Строки);
	КонецЕсли;
	
	
	
	Результат.Очистить();
	
	Макет = Отчеты.БюджетДДС.ПолучитьМакет("Макет");
	
	Область = Макет.ПолучитьОбласть("Шапка|Статья");
	Результат.Вывести(Область);
	Для сч = 1 По 12 Цикл
		
		Область = Макет.ПолучитьОбласть("Шапка|Суммы"+?(ПоляАбс, "Абс", ""));
		Область.Параметры.Месяц = ОбщийМодуль1Сервер.ИмяМесяцаПоНомеру(сч);
		Результат.Присоединить(Область);
		
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Шапка|Суммы"+?(ПоляАбс, "Абс", ""));
	Область.Параметры.Месяц = "ИТОГО";
	Результат.Присоединить(Область);
	
	//вывод дерева отчета
	Для каждого Стр Из ДеревоБДР.Строки Цикл
		ВывестиСтрокуДереваВОтчет(Стр, Макет);
	КонецЦикла;
	
	//убирает надпись "Отчет не сформирован"
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения  = ДополнительныйРежимОтображения.НеИспользовать;
	Элементы.Результат.ОтображениеСостояния.Видимость  = Ложь;
	
	//Результат.ПоказатьУровеньГруппировокСтрок(1);
	
	Результат.ФиксацияСверху = 2;
	Результат.ФиксацияСлева = 1;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура РасчетАбс(Строки)
	Для каждого Стр Из Строки Цикл
		Для сч = 1 По 13 Цикл
			Стр["Абс"+сч] = Стр["Факт"+сч]-Стр["План"+сч];
		КонецЦикла;
		РасчетАбс(Стр.Строки);
	КонецЦикла;
КонецПроцедуры


&НаСервере
Процедура ВывестиСтрокуДереваВОтчет(Стр, Макет)
	
	Область = Макет.ПолучитьОбласть("Строка"+?(Стр["ЭтоГруппа"], "Г", "")+"|Статья");
	Область.Параметры.СтатьяДР = ОбщийМодуль1Сервер.ДополнитьСтроку("","  ",Стр.Уровень())+Стр.СтатьяДР;
	Результат.Вывести(Область);
	
	Для сч = 1 По 13 Цикл
		
		Область = Макет.ПолучитьОбласть("Строка"+?(Стр["ЭтоГруппа"], "Г", "")+"|Суммы"+?(ПоляАбс, "Абс", ""));
		Область.Параметры.План = Стр["План"+сч];
		Область.Параметры.Факт = Стр["Факт"+сч];
		Область.Параметры.Рез  = Стр["Рез" +сч];
		Если ПоляАбс Тогда
			Область.Параметры.Абс  = Стр["Абс" +сч];
		КонецЕсли;
		
		Результат.Присоединить(Область);
		
	КонецЦикла;
	
	Результат.НачатьГруппуСтрок(, Ложь);
	
	Для каждого Стр1 Из Стр.Строки Цикл
		
		ВывестиСтрокуДереваВОтчет(Стр1, Макет);
		
	КонецЦикла;
	
	Результат.ЗакончитьГруппуСтрок();
	
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьОтчет(Команда)
	Если НЕ ЗначениеЗаполнено(План) Тогда
		Предупреждение("Необходимо выбрать плановый сценарий!");	
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	Рез = СформироватьОтчетСервер(ТекстОшибки);
	Если НЕ Рез Тогда
		Предупреждение(ТекстОшибки, 120);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПоляПлан = Истина;
	ПоляФакт = Истина;
	ПоляРез = Истина;
КонецПроцедуры

