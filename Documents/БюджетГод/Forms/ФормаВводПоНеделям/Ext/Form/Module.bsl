//реквизиты формы
//ТабПериодовНеделя
//колонки
//НомерМесяца - число, номер месяца в году
//НомерНедели - число, номер недели в месяца (т.е. от 1 до 6)
//Начало - дата, начало недели
//Конец - дата, конец недели

//заполняет таблицу строками, в которых указан номер месяца, 
//номер недели в месяце, даты: начало и конец недели
&НаКлиенте
Процедура ЗаполнитьТаблицуПериодовНеделя()
	ТекДата = НачалоГода(Дата);
	КонГода = КонецГода(ТекДата)+1;
	НомерНедели  = 1;
	НомерМес = 1;
	Начало = ТекДата;
	Пока ТекДата<КонГода Цикл
		
		Если ДеньНедели(ТекДата) = 7 ИЛИ КонецМесяца(ТекДата) = КонецДня(ТекДата) Тогда
			ДобавитьНеделю(НомерМес, НомерНедели, Начало, ТекДата);
			Если КонецМесяца(ТекДата) = КонецДня(ТекДата) Тогда
				//это воскресенье - последний день месяца
				НомерМес = НомерМес + 1;
				НомерНедели  = 1;
			Иначе
				НомерНедели = НомерНедели + 1;
			КонецЕсли;
				
			Начало = ТекДата + 86400;
		КонецЕсли;
				
		ТекДата = ТекДата + 86400;
	КонецЦикла;
	
КонецПроцедуры

//строку в таблицу "ТабПериодовНеделя"
&НаКлиенте
Процедура ДобавитьНеделю(НомерМес, НомерНедели, Начало, Конец)
	НовСтр = ТабПериодовНеделя.Добавить();
	НовСтр.НомерМесяца = НомерМес;
	НовСтр.НомерНедели = НомерНедели;
	НовСтр.Начало = Начало;
	НовСтр.Конец = Конец;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьТаблицуПериодовНеделя();
	//скрыть лишние столбцы
	ПоискСтрок = ТабПериодовНеделя.НайтиСтроки(Новый Структура("НомерМесяца",НомерМесяца));
	Для сч = 1 по 6 Цикл
		Элементы["ТабДанныхНеделя"+сч].Видимость=Ложь;
	КонецЦикла;
	Для Каждого Эл Из ПоискСтрок Цикл
		Элементы["ТабДанныхНеделя"+Эл.НомерНедели].Видимость=Истина;
		Элементы["ТабДанныхНеделя"+Эл.НомерНедели].Заголовок=Формат(Эл.НАчало,"ДФ=дд")+" - "+Формат(Эл.Конец, "ДФ=дд");
	КонецЦикла;
	ТабДанных.Очистить();
	ЗаполнитьКошелькиСервер();
	ЗаполнитьДанныеПрогноз();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКошелькиСервер()
	ТЗ = ДанныеФормыВЗначение(ТабДанных, тип("ТаблицаЗначений"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Кошельки.Ссылка,
		|	Кошельки.Валюта
		|ИЗ
		|	Справочник.Кошельки КАК Кошельки
		|ГДЕ
		|	Кошельки.НедельноеПланирование
		|
		|УПОРЯДОЧИТЬ ПО
		|	Кошельки.Наименование";

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НовСтр = ТЗ.Добавить();
		НовСтр.Кошелек = Выборка.Ссылка;
		НовСтр.Валюта = Выборка.Валюта;

	КонецЦикла;


	ЗначениеВДанныеФормы(ТЗ, ТабДанных);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьДанныеПрогноз()
	
	ЗаполнитьДанныеПрогнозСервер();	
	ПересчитатьИтог();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПрогнозСервер()
	ТЗ = ДанныеФормыВЗначение(ТабДанных, тип("ТаблицаЗначений"));
	
	ПоискСтрок = ТабПериодовНеделя.НайтиСтроки(Новый Структура("НомерМесяца",НомерМесяца));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Сумма КАК Сумма,
		|	Док.Дата КАК Дата,
		|	Док.Кошелек,
		|	Док.Ссылка
		|ИЗ
		|	Документ.РасходСредств КАК Док
		|ГДЕ
		|	Док.СтатьяДР = &СтатьяДР
		|	И Док.Дата МЕЖДУ &ДатаНач И &ДатаКон
		|	И Док.ЭтоПрогноз
		|	И Док.Бюджет = &Бюджет";

	Запрос.УстановитьПараметр("ДатаНач", ПоискСтрок[0].Начало);
	Запрос.УстановитьПараметр("ДатаКон", ПоискСтрок[ПоискСтрок.Количество()-1].Конец);
	Запрос.УстановитьПараметр("СтатьяДР", СтатьяДР);
	Запрос.УстановитьПараметр("Бюджет", Бюджет);

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		Для Каждого Эл из ПоискСтрок Цикл
			//документы прогноза только в начале недели или в первом дне месяца
			//Если Выборка.Дата>=ЭЛ.Начало И Выборка.Дата<Эл.Конец Тогда
			Если Выборка.Дата=ЭЛ.Начало Тогда
				//сначала поиск строки с кошельком
				ПС = ТЗ.Найти(Выборка.Кошелек, "Кошелек");
				Если НЕ ПС=Неопределено Тогда
					ПС["Неделя"+ЭЛ.НомерНедели] = Выборка.Сумма;
					ПС["Док"+ЭЛ.НомерНедели] = Выборка.Ссылка;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	ЗначениеВДанныеФормы(ТЗ, ТабДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ТабДанныхПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПересчитатьИтог();
КонецПроцедуры


&НаКлиенте
Процедура ПересчитатьИтог()
	Итого=0;
	Для Каждого Стр Из ТАбДанных Цикл
		Стр.Итого=0;
		Для сч = 1 по 6 цикл
			Итого = Итого+Стр["Неделя"+сч];
			Стр.Итого= Стр.Итого+Стр["Неделя"+сч];
		КонецЦИкла;
	КонецЦИкла;

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	//записать и закрыть
	СоздатьДДС();
	Закрыть(Итого);	
КонецПроцедуры

&НаСервере
Процедура СоздатьДДС()
	
	//получаем таблицу с формы
	ТЗ = ДанныеФормыВЗначение(ТабДанных, тип("ТаблицаЗначений"));
	
	//ищем строки с нужным месяцем в большой таблице периодов (за весь год)
	ПоискСтрок = ТабПериодовНеделя.НайтиСтроки(Новый Структура("НомерМесяца",НомерМесяца));
	
	Для Каждого Стр Из ТЗ Цикл
		//если нет плана на какую-то неделю - пропускаем строку
		Если Стр.Итого=0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для сч = 1 по 6 цикл
			//есил сумма 0 и документа нет, ничего не делаем
			Если Стр["Неделя"+сч]=0 И НЕ ЗначениеЗаполнено(Стр["Док"+сч]) Тогда   
				Продолжить;
			//если что-то (ссылка над документ) есть в служебной колонке "ДокХХ" -
			//обновляем этот документ
			ИначеЕсли ЗначениеЗаполнено(Стр["Док"+сч]) Тогда
				ДокОбъект 			= Стр["Док"+сч].ПолучитьОбъект();
				ДокОбъект.Сумма 	= Стр["Неделя"+сч] ;
				ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);

			//если служебная колонка "ДокХХ" пустая, но есть план - создать документ
			ИначеЕсли НЕ Стр["Неделя"+сч] =0 И НЕ ЗначениеЗаполнено(Стр["Док"+сч]) Тогда
				//документа нет - надо создать
				
				ДокОбъект 				= Документы.РасходСредств.СоздатьДокумент();
				ДокОбъект.Бюджет		= Бюджет;
				ДокОбъект.Дата 			= ПоискСтрок[сч-1].Начало;
				ДокОбъект.Сумма 		= Стр["Неделя"+сч] ;
				ДокОбъект.Кошелек 		= Стр.Кошелек;
				ДокОбъект.ПриИзмененииКошелька(); 
				ДокОбъект.СтатьяДР 		= СтатьяДР;
				ДокОбъект.ЭтоПрогноз 	= Истина;
				ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);

			КонецЕсли;
			
		КонецЦИкла;
	КонецЦИкла;
	
КонецПроцедуры


&НаКлиенте
Процедура Отмена(Команда)
	//просто закрыть
	Закрыть(Неопределено);
КонецПроцедуры
