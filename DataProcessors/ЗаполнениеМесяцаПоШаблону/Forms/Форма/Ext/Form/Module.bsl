
//Заполняет таблицы значений на форме статьями и суммами из годового бюджета
//даты для колонки Период берутся из регистра сведений ПериодыДляПериодичныхСтатей
//если там не обнаружено даты, то статья попадает в общую таблицу
&НаКлиенте
Процедура ЗаполнитьСтатьи(Команда)
	ЗаполнитьСтатьиС();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатьиС()
	
	//получаем таблицы значений с формы в значения
	ТЗГодФорма = ДанныеФормыВЗначение(ТЗГод,Тип("ТаблицаЗначений"));
	ТЗГодФорма.Очистить();
	
	ТЗМесяцФорма = ДанныеФормыВЗначение(ТЗМесяц,Тип("ТаблицаЗначений"));
	ТЗМесяцФорма.Очистить();
	
	ТЗНеделяФорма = ДанныеФормыВЗначение(ТЗНеделя,Тип("ТаблицаЗначений"));
	ТЗНеделяФорма.Очистить();
	
	ТЗРабДеньФорма = ДанныеФормыВЗначение(ТЗРабДень,Тип("ТаблицаЗначений"));
	ТЗРабДеньФорма.Очистить();
	
	ТЗДеньФорма = ДанныеФормыВЗначение(ТЗДень,Тип("ТаблицаЗначений"));
	ТЗДеньФорма.Очистить();
	
	КэшПериодов = ПолучитьКэшПериодовСтатей();//Это ТЗ
	
	//1. выбрать все из постоянных расходов
	//там указана сумма на период расхода (день, месяц)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Рег.Статья КАК Статья,
		|	Рег.Сумма КАК Сумма,
		|	Рег.Статья.Периодичность КАК Периодичность
		|ИЗ
		|	РегистрСведений.ПостоянныеРасходы.СрезПоследних(&ВыбПериод, ) КАК Рег";

	Запрос.УстановитьПараметр("ВыбПериод", ВыбМесяц);

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	МассивПонедельников = ПолучитьМассивПонедельников();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Периодичность = Перечисления.ПериодичностьСтатьи.Год Тогда
			
			
			СтрПериод = КэшПериодов.Найти(Выборка.Статья, "Статья");
			Если СтрПериод <> Неопределено Тогда
				
			    //если текущий месяц не равен месяцу из периодичности, то не добавляем эту статю в годовую таблицу
				Если Месяц(ВыбМесяц)<>Месяц(СтрПериод.Период) Тогда
					Продолжить;
				КонецЕсли;
				
				НовСтр = ТЗГодФорма.Добавить();
				НовСтр.Статья = Выборка.Статья;
				НовСтр.Сумма = Выборка.Сумма;
				
				НовСтр.Период = Дата(Год(ВыбМесяц), Месяц(СтрПериод.Период), День(СтрПериод.Период));
				
			Иначе
				Продолжить;
			КонецЕсли;
			
		ИначеЕсли Выборка.Периодичность = Перечисления.ПериодичностьСтатьи.Месяц Тогда
			НовСтр = ТЗМесяцФорма.Добавить();
			НовСтр.Период = "";
			НовСтр.Статья = Выборка.Статья;
			НовСтр.Сумма = Выборка.Сумма;
			
			СтрПериод = КэшПериодов.Найти(Выборка.Статья, "Статья");
			Если СтрПериод <> Неопределено Тогда
				НовСтр.Период = Дата(Год(ВыбМесяц), Месяц(ВыбМесяц), День(СтрПериод.Период));
			КонецЕсли;
			
		ИначеЕсли Выборка.Периодичность = Перечисления.ПериодичностьСтатьи.Неделя Тогда
			
			Для Каждого Стр Из МассивПонедельников Цикл
				НовСтр = ТЗНеделяФорма.Добавить();
				НовСтр.Период = Стр;
				НовСтр.Статья = Выборка.Статья;
				НовСтр.Сумма = Выборка.Сумма;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;

	ТЗГодФорма.сортировать("Период, Статья");
	ТЗМесяцФорма.сортировать("Период, Статья");
	ТЗНеделяФорма.сортировать("Период, Статья");
	
	
	//отправляем заполнненные таблицы значений обратно на форму
	//					  имя переменной    имя на форме
	ЗначениеВДанныеФормы(ТЗГодФорма, 		ТЗГод);
	ЗначениеВДанныеФормы(ТЗМесяцФорма, 		ТЗМесяц);
	ЗначениеВДанныеФормы(ТЗНеделяФорма, 	ТЗНеделя);
	ЗначениеВДанныеФормы(ТЗРабДеньФорма,	ТЗРабДень);
	ЗначениеВДанныеФормы(ТЗДеньФорма, 		ТЗДень);
	
КонецПроцедуры


//возвращает таблицу значений, в которой находится кэш периодов:
//2 колонки: Статья, Период
//в этой таблице ищем статью при заполнении таблиц на форме и если нашли:
//для периодичности Месяц - берем день периода, месяц и год периода берем из месяца заполнения.
//для периодичности Год: берем день и месяц. год берем из месяца заполнения
//для периодичности ___:
&НаСервере
Функция ПолучитьКэшПериодовСтатей()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Рег.Период КАК Период,
		|	Рег.Статья КАК Статья
		|ИЗ
		|	РегистрСведений.ПериодыДляПериодичныхСтатей КАК Рег";

	Запрос.УстановитьПараметр("ВыбПериод", ВыбМесяц);

	Результат = Запрос.Выполнить();
	ТЗ = Результат.Выгрузить();
	ТЗ.Индексы.Добавить("Статья");

	Возврат ТЗ;

	
	
КонецФункции

&НаСервере
Функция ПолучитьМассивПонедельников()
		//нужно понять, сколько в месяце недель
			//неделю будем считать по понедельникам (для удобства)
			
			НачалоНедели = 1;//день в месяце. отправная точка
			ДатаНачалаНедели = Дата(Год(ВыбМесяц), Месяц(ВыбМесяц), НачалоНедели);
			
			МассивПонедельников = Новый Массив;
			
			Если ДеньНедели(ДатаНачалаНедели) = 1 Тогда
				//нашли с первой попытки
			Иначе
				//ищем первый понедельник после ДатаНачалаНедели
				Для сч = 1 по 30 Цикл
					
					ДатаНачалаНедели = Дата(Год(ВыбМесяц), Месяц(ВыбМесяц), НачалоНедели+сч);
					Если ДеньНедели(ДатаНачалаНедели) = 1 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Пока 1=1 Цикл
				Если КонецДня(ДатаНачалаНедели) > КонецМесяца(ВыбМесяц) Тогда
					Прервать;
				КонецЕсли;
				МассивПонедельников.Добавить(ДатаНачалаНедели);
				ДатаНачалаНедели = ДатаНачалаНедели + 7*86400;
			КонецЦикла;
			
	Возврат МассивПонедельников;
КонецФункции